<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="T_TestRunner" Id="{c7f35beb-7079-466d-ae1a-1beb011dea9f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK T_TestRunner IMPLEMENTS I_TestRunner
VAR
    State : (
        sInit,
        sGetTest,
        sRunTest,
        sCheckResults
    );
    TestSuiteName    : T_MaxString := 'TestSuite';
    Verbose          : BOOL;
	StopOnFailedTest : BOOL;
    ResultsSummary   : T_MaxString;
    PassedTestsCount : DINT;
    FailedTestsCount : DINT;
    TestList         : T_ListOfTests(Capacity := 50);
    TestIterator     : T_TestIterator;
    CurrentTest      : I_UnitTest;

    RunTests           : BOOL := TRUE;
    TestFailed         : BOOL;
    TestCompleted      : BOOL;
    _AllTestsCompleted : BOOL;
    _AllTestsPassed    : BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{35bbb33e-58f9-46f6-83b2-7eeb5a0d79dc}" />
    <Folder Name="Properties" Id="{7af4ba9e-2e23-48af-83d2-a78ec9cce9dd}" />
    <Method Name="AddTest" Id="{aff2d568-ab55-47bc-af2b-94e85ba5e218}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD AddTest
VAR_INPUT
    UnitTest : I_UnitTest;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TestList.AddTest(UnitTest);
]]></ST>
      </Implementation>
    </Method>
    <Property Name="AllTestsCompleted" Id="{f1cfa0dd-9b7a-4a7e-85d3-02076a295709}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY AllTestsCompleted : BOOL]]></Declaration>
      <Get Name="Get" Id="{e62d03c9-e800-4a62-a3b1-ac1d1cba6068}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[AllTestsCompleted := _AllTestsCompleted;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="AllTestsPassed" Id="{eeaf1b8a-bc87-4949-bb30-c49a3beae881}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY AllTestsPassed : BOOL]]></Declaration>
      <Get Name="Get" Id="{93a12b30-a161-4216-91b0-2c189234f715}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[AllTestsPassed := _AllTestsPassed;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ClearTests" Id="{9c636768-dc92-4c10-a951-416ca0c5943a}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD ClearTests
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[TestList.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="CyclicUpdate" Id="{5c3a5d41-6805-496d-ba38-305df4534527}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD CyclicUpdate
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE State OF
    sInit:
        IF RunTests THEN
            RunTests := FALSE;
            TestIterator.Initialise(TestList);
            ResultsSummary     := '';
            FailedTestsCount   := 0;
            PassedTestsCount   := 0;
            _AllTestsCompleted := FALSE;
            F_LogResult(Source := TestSuiteName, Message := 'Starting Tests');
            State := sGetTest;
        END_IF

    sGetTest:
        IF TestIterator.HasMore() THEN
            CurrentTest := TestIterator.GetNext();
            CurrentTest.Init();
            State := sRunTest;
        ELSE
            CurrentTest := 0;
            State       := sCheckResults;
        END_IF

    sRunTest:
        CurrentTest.RunTest(TestCompleted => TestCompleted,
                            TestFailed    => TestFailed);

        IF TestCompleted THEN
            IF TestFailed THEN
                FailedTestsCount := FailedTestsCount + 1;
                F_LogResult(Source := TestSuiteName, Message := FormatStringEx1('Test failed <%s>', CurrentTest.Name));
            ELSE
                PassedTestsCount := PassedTestsCount + 1;
                IF Verbose THEN
                    F_LogResult(Source := TestSuiteName, Message := FormatStringEx1('Test passed <%s>', CurrentTest.Name));
                END_IF
            END_IF

            IF TestFailed AND StopOnFailedTest THEN
                State := sCheckResults;
            ELSE
                State := sGetTest;
            END_IF

        END_IF

    sCheckResults:
        ResultsSummary := FormatString2('Tests completed. %d Tests Passed, %d Tests Failed',
                                        F_DINT(PassedTestsCount),
                                        F_DINT(FailedTestsCount));

        F_LogResult(Source  := TestSuiteName,
                    Message := FormatString2('Tests completed. %d Tests Passed, %d Tests Failed',
                                             F_DINT(PassedTestsCount),
                                             F_DINT(FailedTestsCount)));
        _AllTestsCompleted := TRUE;
        _AllTestsPassed    := (FailedTestsCount = 0);
        State              := sInit;

END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{4d8559a3-1b79-40c6-ae87-d14857fb3a8a}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
    bInitRetains     : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode      : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    TestSuiteName    : T_MaxString := 'TestSuite';
    Verbose          : BOOL;
    StopOnFailedTest : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.TestSuiteName    := TestSuiteName;
THIS^.Verbose          := Verbose;
THIS^.StopOnFailedTest := StopOnFailedTest;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Start" Id="{78778059-eb1a-48b2-a0cc-e7bff17e395e}" FolderPath="Methods\">
      <Declaration><![CDATA[{attribute 'call_after_online_change_slot' := '200'}
METHOD Start
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[RunTests := TRUE;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>